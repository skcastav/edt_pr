
&НаКлиенте
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
СтандартнаяОбработка = Ложь;
Ф = ПолучитьФорму("ОбщаяФорма.ВыборМесяца");
Результат = Ф.ОткрытьМодально();
	Если Результат <> Неопределено Тогда
	Отчет.Период.ДатаНачала = Результат;
	Отчет.Период.ДатаОкончания = КонецМесяца(Результат);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция РасчитатьСебестоимость(Спецификация,КолУзла,Стоимость)
НаДату = Отчет.Период.ДатаОкончания;
Запрос = Новый Запрос;

Запрос.Текст = 
	"ВЫБРАТЬ
	|	НормыРасходов.Элемент,
	|	НормыРасходовСрезПоследних.Норма,
	|	НормыРасходов.Ссылка,
	|	НормыРасходов.ВидЭлемента
	|ИЗ
	|	РегистрСведений.НормыРасходов.СрезПоследних(&НаДату, ) КАК НормыРасходовСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НормыРасходов КАК НормыРасходов
	|		ПО НормыРасходовСрезПоследних.НормаРасходов = НормыРасходов.Ссылка
	|ГДЕ
	|	НормыРасходов.ПометкаУдаления = ЛОЖЬ
	|	И НормыРасходов.Владелец = &Владелец
	|	И (ТИПЗНАЧЕНИЯ(НормыРасходов.Элемент) = ТИП(Справочник.Номенклатура)
	|			ИЛИ ТИПЗНАЧЕНИЯ(НормыРасходов.Элемент) = ТИП(Справочник.Материалы))
	|	И НормыРасходовСрезПоследних.Норма > 0";
Запрос.УстановитьПараметр("НаДату", НаДату);
Запрос.УстановитьПараметр("Владелец", Спецификация);
РезультатЗапроса = Запрос.Выполнить();
ВыборкаНР = РезультатЗапроса.Выбрать();
	Пока ВыборкаНР.Следующий() Цикл
	БазовоеКоличество = ПолучитьБазовоеКоличествоБезОкругления(ВыборкаНР.Норма,ВыборкаНР.Элемент.ОсновнаяЕдиницаИзмерения)*КолУзла;			
		Если ТипЗнч(ВыборкаНР.Элемент) = Тип("СправочникСсылка.Материалы")Тогда
		Стоимость = Стоимость + ОбщийМодульВызовСервера.ПолучитьПоследнююЦену(ВыборкаНР.Элемент,НаДату)*БазовоеКоличество;
		ИначеЕсли ТипЗнч(ВыборкаНР.Элемент) = Тип("СправочникСсылка.Номенклатура")Тогда
			Если ВыборкаНР.Элемент.Канбан.Пустая() Тогда
			РасчитатьСебестоимость(ВыборкаНР.Элемент,БазовоеКоличество,Стоимость);			
			Иначе
			ТЧ = ТаблицаКанбанов.Добавить();
			ТЧ.Подразделение = ВыборкаНР.Элемент.Канбан.ПодразделениеДляСебестоимости;						
			ТЧ.МПЗ = ВыборкаНР.Элемент;
			ТЧ.Количество = БазовоеКоличество;
			Себестоимости = РегистрыСведений.Себестоимость.ПолучитьПоследнее(Отчет.Период.ДатаНачала,Новый Структура("Подразделение,Линейка,Номенклатура",ВыборкаНР.Элемент.Канбан.ПодразделениеДляСебестоимости,Справочники.Линейки.ПустаяСсылка(),ВыборкаНР.Элемент));
				Если СписокПодразделений.НайтиПоЗначению(ВыборкаНР.Элемент.Канбан.ПодразделениеДляСебестоимости) = Неопределено Тогда
				ТЧ.Себестоимость = Себестоимости.СебестоимостьПолная*БазовоеКоличество; 
				Иначе
				СтоимостьМатериалов	= РасчитатьСебестоимость(ВыборкаНР.Элемент,БазовоеКоличество,0);
				ТЧ.СтоимостьМатериалов = СтоимостьМатериалов;
				КС = РегистрыСведений.КоэффициентыСебестоимости.ПолучитьПоследнее(Отчет.Период.ДатаНачала,Новый Структура("Подразделение,Линейка",ВыборкаНР.Элемент.Канбан.ПодразделениеДляСебестоимости,Справочники.Линейки.ПустаяСсылка()));
				ТЧ.СтоимостьМатериаловТехПотери = СтоимостьМатериалов*КС.КоэффициентТехнологическихПотерь;
				ТЧ.ЗарплатнаяЧасть = Себестоимости.ЗарплатнаяЧасть*БазовоеКоличество;
				ТЧ.НакладныеРасходы = Себестоимости.ЗарплатнаяЧасть*КС.КоэффициентНакладныхРасходов; 
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
Возврат(Стоимость);
КонецФункции

&НаСервере
Процедура СформироватьНаСервере()
ТабДок.Очистить();
ОбъектЗн = РеквизитФормыВЗначение("Отчет");
Макет = ОбъектЗн.ПолучитьМакет("Макет");

ОблШапка = Макет.ПолучитьОбласть("Шапка");
ОблЛинейка = Макет.ПолучитьОбласть("Линейка");
ОблИзделие = Макет.ПолучитьОбласть("Изделие");
ОблПодразделение = Макет.ПолучитьОбласть("Подразделение");
ОблПФ = Макет.ПолучитьОбласть("ПФ");
ОблКонец = Макет.ПолучитьОбласть("Конец");
 
ОблШапка.Параметры.Период = Формат(Отчет.Период.ДатаОкончания,"ДФ=ММММгггг");
ТабДок.Вывести(ОблШапка);

Запрос = Новый Запрос;

Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПланыВыпускаОбороты.Номенклатура,
	|	ПланыВыпускаОбороты.КоличествоРасход КАК Количество,
	|	ПланыВыпускаОбороты.Линейка.Родитель КАК Линейка
	|ИЗ
	|	РегистрНакопления.ПланыВыпуска.Обороты(&ДатаНач, &ДатаКон, Регистратор, ) КАК ПланыВыпускаОбороты
	|ГДЕ
	|	ПланыВыпускаОбороты.МаршрутнаяКарта.Ремонт = ЛОЖЬ
	|	И ПланыВыпускаОбороты.Линейка.Родитель В(&СписокЛинеек)";
Запрос.УстановитьПараметр("ДатаНач", НачалоДня(Отчет.Период.ДатаНачала));
Запрос.УстановитьПараметр("ДатаКон", КонецДня(Отчет.Период.ДатаОкончания));
Запрос.УстановитьПараметр("СписокЛинеек", СписокЛинеек);
	Если СписокНоменклатуры.Количество() > 0 Тогда
	Запрос.Текст = Запрос.Текст + " И ПланыВыпускаОбороты.Номенклатура В ИЕРАРХИИ(&СписокНоменклатуры)";
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	КонецЕсли;  
Запрос.Текст = Запрос.Текст + " УПОРЯДОЧИТЬ ПО ПланыВыпускаОбороты.Линейка.Родитель.Наименование,ПланыВыпускаОбороты.Номенклатура.Родитель.Наименование
								| ИТОГИ СУММА(Количество) ПО ПланыВыпускаОбороты.Линейка.Родитель,ПланыВыпускаОбороты.Номенклатура";
РезультатЗапроса = Запрос.Выполнить();
ВыборкаЛинейки = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЛинейки.Следующий() Цикл
	ОблЛинейка.Параметры.Линейка = ВыборкаЛинейки.Линейка;
	ТабДок.Вывести(ОблЛинейка);
	ТабДок.НачатьГруппуСтрок("Линейка",Истина);
	ВыборкаИзделия = ВыборкаЛинейки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаИзделия.Следующий() Цикл
		ТаблицаКанбанов.Очистить();
		ТЧ = ТаблицаКанбанов.Добавить();
		ТЧ.Подразделение = ВыборкаЛинейки.Линейка.Подразделение;						
		ТЧ.МПЗ = ВыборкаИзделия.Номенклатура;
		ТЧ.Количество = 1;
		ТЧ.СтоимостьМатериалов = РасчитатьСебестоимость(ВыборкаИзделия.Номенклатура,1,0);
		КС = РегистрыСведений.КоэффициентыСебестоимости.ПолучитьПоследнее(Отчет.Период.ДатаНачала,Новый Структура("Подразделение,Линейка",ВыборкаЛинейки.Линейка.Подразделение,ВыборкаЛинейки.Линейка));
		ТЧ.СтоимостьМатериаловТехПотери = ТЧ.СтоимостьМатериалов*КС.КоэффициентТехнологическихПотерь;
		Себестоимости = РегистрыСведений.Себестоимость.ПолучитьПоследнее(Отчет.Период.ДатаНачала,Новый Структура("Подразделение,Линейка,Номенклатура",ВыборкаЛинейки.Линейка.Подразделение,ВыборкаЛинейки.Линейка,ВыборкаИзделия.Номенклатура));
		ТЧ.ЗарплатнаяЧасть = Себестоимости.ЗарплатнаяЧасть;
		ТЧ.НакладныеРасходы = Себестоимости.ЗарплатнаяЧасть*КС.КоэффициентНакладныхРасходов;
		ГруппаТО = Справочники.ТехОперации.НайтиПоНаименованию("Производство приборов");
		ТО = Справочники.ТехОперации.НайтиПоНаименованию("СТ",Истина,ГруппаТО);
		НормыВремени = РегистрыСведений.НормыВремени.ПолучитьПоследнее(Отчет.Период.ДатаНачала,Новый Структура("МПЗ,ТехОперация",ВыборкаИзделия.Номенклатура,ТО));
		ТЧ.НормаВремени = НормыВремени.НормаВремени;
		ТЧ.НакладныеРасходыСТК = НормыВремени.НормаВремени*КС.КоэффициентНакладныхРасходовСТК;
	
		ТЗ = ТаблицаКанбанов.Выгрузить();
		ТЗ.Свернуть("Подразделение,МПЗ","Количество,СтоимостьМатериалов,СтоимостьМатериаловТехПотери,ЗарплатнаяЧасть,НакладныеРасходы,НормаВремени,НакладныеРасходыСТК,Себестоимость");
		ТЗ.Сортировать("Подразделение,МПЗ");
		ТЗ_Подр = ТаблицаКанбанов.Выгрузить();
		ТЗ_Подр.Свернуть("Подразделение","Количество,СтоимостьМатериалов,СтоимостьМатериаловТехПотери,ЗарплатнаяЧасть,НакладныеРасходы,НормаВремени,НакладныеРасходыСТК,Себестоимость");
		ТЗ_Подр.Сортировать("Подразделение");

		ОблИзделие.Параметры.Наименование = СокрЛП(ВыборкаИзделия.Номенклатура.Наименование);
		ОблИзделие.Параметры.Изделие = ВыборкаИзделия.Номенклатура;
		ОблИзделие.Параметры.Количество = ВыборкаИзделия.Количество;
		ОблИзделие.Параметры.ЕдиницаИзмерения = СокрЛП(ВыборкаИзделия.Номенклатура.ЕдиницаИзмерения.Наименование);
		ОблИзделие.Параметры.СтоимостьМатериалов = ТЗ_Подр.Итог("СтоимостьМатериалов");
		ОблИзделие.Параметры.СтоимостьМатериаловТехПотери = ТЗ_Подр.Итог("СтоимостьМатериаловТехПотери");
		ОблИзделие.Параметры.ЗарплатнаяЧасть = ТЗ_Подр.Итог("ЗарплатнаяЧасть");
		КС = РегистрыСведений.КоэффициентыСебестоимости.ПолучитьПоследнее(Отчет.Период.ДатаНачала,Новый Структура("Подразделение,Линейка",ВыборкаЛинейки.Линейка.Подразделение,ВыборкаЛинейки.Линейка));
		ЗарплатнаяЧасть = ТЗ_Подр.Итог("ЗарплатнаяЧасть");
		ЕСН = ЗарплатнаяЧасть*Константы.КоэффициентЕСН.Получить();
		ОблИзделие.Параметры.ЕСН = ЕСН;
 		НакладныеРасходы = ТЗ_Подр.Итог("НакладныеРасходы");
		ОблИзделие.Параметры.НакладныеРасходы = НакладныеРасходы;
 		НакладныеРасходыСТК = ТЗ_Подр.Итог("НакладныеРасходыСТК");
		ОблИзделие.Параметры.НакладныеРасходыСТК = НакладныеРасходыСТК;
		Себестоимость = ТЗ_Подр.Итог("СтоимостьМатериаловТехПотери") + ТЗ_Подр.Итог("ЗарплатнаяЧасть") + ЕСН + НакладныеРасходы + НакладныеРасходыСТК + ТЗ_Подр.Итог("Себестоимость");
		ОблИзделие.Параметры.Себестоимость = Себестоимость;
		ОблИзделие.Параметры.СебестоимостьВыпуска = Себестоимость*ВыборкаИзделия.Количество;
		ТабДок.Вывести(ОблИзделие);
		ТабДок.НачатьГруппуСтрок("Изделие",Истина);

		ТекПодразделение = Неопределено;
			Для каждого ТЧ Из ТЗ Цикл
				Если ТекПодразделение <> ТЧ.Подразделение  Тогда
					Если ТекПодразделение <> Неопределено Тогда
					ТабДок.ЗакончитьГруппуСтрок();
					КонецЕсли;
				Выборка_Подр = ТЗ_Подр.Найти(ТЧ.Подразделение,"Подразделение");
					Если ВыборкаЛинейки.Линейка.Подразделение = Выборка_Подр.Подразделение Тогда
					КС = РегистрыСведений.КоэффициентыСебестоимости.ПолучитьПоследнее(Отчет.Период.ДатаНачала,Новый Структура("Подразделение,Линейка",Выборка_Подр.Подразделение,ВыборкаЛинейки.Линейка));
					Иначе	
					КС = РегистрыСведений.КоэффициентыСебестоимости.ПолучитьПоследнее(Отчет.Период.ДатаНачала,Новый Структура("Подразделение,Линейка",Выборка_Подр.Подразделение,Справочники.Линейки.ПустаяСсылка()));
					КонецЕсли;
				ОблПодразделение.Параметры.Подразделение = Выборка_Подр.Подразделение;
				ОблПодразделение.Параметры.СтоимостьМатериалов = Выборка_Подр.СтоимостьМатериалов;
				ОблПодразделение.Параметры.КТП = КС.КоэффициентТехнологическихПотерь;  
				ОблПодразделение.Параметры.СтоимостьМатериаловТехПотери = Выборка_Подр.СтоимостьМатериаловТехПотери;
				ОблПодразделение.Параметры.ЗарплатнаяЧасть = Выборка_Подр.ЗарплатнаяЧасть;
				ЕСН = Выборка_Подр.ЗарплатнаяЧасть*Константы.КоэффициентЕСН.Получить();
				ОблПодразделение.Параметры.ЕСН = ЕСН;
				ОблПодразделение.Параметры.КНР = КС.КоэффициентНакладныхРасходов;
				ОблПодразделение.Параметры.КНР_СТК = КС.КоэффициентНакладныхРасходовСТК;
				ОблПодразделение.Параметры.НакладныеРасходы = Выборка_Подр.НакладныеРасходы;
				ОблПодразделение.Параметры.НакладныеРасходыСТК = Выборка_Подр.НакладныеРасходыСТК;
					Если СписокПодразделений.НайтиПоЗначению(ТЧ.Подразделение) = Неопределено Тогда
					Себестоимость = Выборка_Подр.Себестоимость;
					Иначе	
					Себестоимость = Выборка_Подр.СтоимостьМатериаловТехПотери+Выборка_Подр.ЗарплатнаяЧасть+ЕСН+Выборка_Подр.НакладныеРасходы+Выборка_Подр.НакладныеРасходыСТК;
					КонецЕсли;
				ОблПодразделение.Параметры.Себестоимость = Себестоимость;
				ОблПодразделение.Параметры.СебестоимостьВыпуска = Себестоимость*ВыборкаИзделия.Количество;				
				ТабДок.Вывести(ОблПодразделение);
				ТабДок.НачатьГруппуСтрок("Подразделение",Ложь);
				ТекПодразделение = ТЧ.Подразделение;			
				КонецЕсли; 
			ОблПФ.Параметры.Парам = Новый Структура("Спецификация,Количество,НаДату,ТолькоКанбан",ТЧ.МПЗ,ТЧ.Количество,Отчет.Период.ДатаОкончания,Истина);
			ОблПФ.Параметры.Наименование = СокрЛП(ТЧ.МПЗ.Наименование);
			ОблПФ.Параметры.ПФ = ТЧ.МПЗ;
			ОблПФ.Параметры.Количество = ТЧ.Количество;
			ОблПФ.Параметры.ЕдиницаИзмерения = СокрЛП(ТЧ.МПЗ.ЕдиницаИзмерения.Наименование);
			ОблПФ.Параметры.СтоимостьМатериалов = ТЧ.СтоимостьМатериалов;
			ОблПФ.Параметры.СтоимостьМатериаловТехпотери = ТЧ.СтоимостьМатериаловТехпотери;
			ОблПФ.Параметры.ЗарплатнаяЧасть = ТЧ.ЗарплатнаяЧасть;
			ЕСН = ТЧ.ЗарплатнаяЧасть*Константы.КоэффициентЕСН.Получить();
			ОблПФ.Параметры.ЕСН = ЕСН;
			ОблПФ.Параметры.НакладныеРасходы = ТЧ.НакладныеРасходы;
			ОблПФ.Параметры.НВ_Стенд = ТЧ.НормаВремени;
			ОблПФ.Параметры.НакладныеРасходыСТК = ТЧ.НакладныеРасходыСТК;
				Если СписокПодразделений.НайтиПоЗначению(ТЧ.Подразделение) = Неопределено Тогда
				Себестоимость = ТЧ.Себестоимость;
				Иначе	
				Себестоимость = ТЧ.СтоимостьМатериаловТехПотери+ТЧ.ЗарплатнаяЧасть+ЕСН+ТЧ.НакладныеРасходы+ТЧ.НакладныеРасходыСТК;
				КонецЕсли;
			ОблПФ.Параметры.Себестоимость = Себестоимость;			
			ОблПФ.Параметры.СебестоимостьВыпуска = Себестоимость*ВыборкаИзделия.Количество;
			ТабДок.Вывести(ОблПФ);
			КонецЦикла;
				Если ТекПодразделение <> Неопределено Тогда
				ТабДок.ЗакончитьГруппуСтрок();
				КонецЕсли;
		ТабДок.ЗакончитьГруппуСтрок();
		КонецЦикла;
	ТабДок.ЗакончитьГруппуСтрок();
	КонецЦикла;
ТабДок.Вывести(ОблКонец);
ТабДок.ФиксацияСверху = 2;
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	Если ЭтаФорма.ПроверитьЗаполнение() Тогда
	СформироватьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДобавитьСебестоимость(Наименование, Себестоимость)
Выборка = Справочники.Номенклатура.Выбрать(,,Новый Структура("Наименование",Наименование));
флНайден = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ПолучитьСтатус(Выборка.Ссылка) <> Перечисления.СтатусыСпецификаций.Запрещённая Тогда
		флНайден = Истина;
		Себестоимости = РегистрыСведений.Себестоимость.СоздатьМенеджерЗаписи();
		Себестоимости.Период = Отчет.Период.ДатаНачала;
		Себестоимости.Подразделение = Выборка.Ссылка.Канбан.ПодразделениеДляСебестоимости;
		Себестоимости.Номенклатура = Выборка.Ссылка.Ссылка;
		Себестоимости.СебестоимостьПолная = Себестоимость;
		Себестоимости.Записать(Истина);
		КонецЕсли; 
	КонецЦикла; 
		Если Не флНайден Тогда
		Сообщить(Наименование + " - не найдена в справочнике!");
		КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСебестоимость(Команда)
Результат = ОбщийМодульКлиент.ОткрытьФайлExcel("Выберите файл c номенклатурой");
	Если Результат <> Неопределено Тогда
	ExcelЛист = Результат.ExcelЛист;
	КолСтрок = Результат.КоличествоСтрок;
	    Для к = 2 по КолСтрок Цикл
		Состояние("Обработка...",к*100/КолСтрок,"Загрузка себестоимости из файла...");
		ДобавитьСебестоимость(СокрЛП(ExcelЛист.Cells(к,1).Value),ExcelЛист.Cells(к,2).Value);
	    КонецЦикла;
	Результат.Excel.Quit();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТабДокОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
ИмяКолонки = СокрЛП(Сред(Элемент.ТекущаяОбласть.Имя,Найти(Элемент.ТекущаяОбласть.Имя,"C")));
НомерКолонки = Число(Сред(ИмяКолонки,2));
	Если НомерКолонки = 4 Тогда
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Отчет.ОтчётПоНормативамРасходов.Форма.ФормаОтчета",Расшифровка);
	КонецЕсли;
КонецПроцедуры
