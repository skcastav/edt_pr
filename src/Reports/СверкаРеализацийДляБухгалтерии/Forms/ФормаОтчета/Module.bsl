
&НаСервере
Процедура СформироватьНаСервере()
ТабДок.Очистить();

ОбъектЗн = РеквизитФормыВЗначение("Отчет");
Макет = ОбъектЗн.ПолучитьМакет("Макет");

ОблШапка = Макет.ПолучитьОбласть("Шапка");
ОблКонтрагент = Макет.ПолучитьОбласть("Контрагент");
ОблМПЗ = Макет.ПолучитьОбласть("МПЗ");
ОблРеализация = Макет.ПолучитьОбласть("Реализация");
ОблИтогоРеализация = Макет.ПолучитьОбласть("ИтогоРеализация");
ОблКонец = Макет.ПолучитьОбласть("Конец");

ОблШапка.Параметры.ДатаНач = НачалоДня(Отчет.Период.ДатаНачала);
ОблШапка.Параметры.ДатаКон = КонецДня(Отчет.Период.ДатаОкончания);
ОблШапка.Параметры.Подразделение = Отчет.Подразделение;
ТабДок.Вывести(ОблШапка);

Запрос = Новый Запрос;
ЗапросМХ = Новый Запрос;
ТаблицаСверки = Новый ТаблицаЗначений;
Массив = Новый Массив;

Массив.Добавить(Тип("СправочникСсылка.Номенклатура"));
Массив.Добавить(Тип("СправочникСсылка.Материалы"));
ОписаниеТиповСправочников = Новый ОписаниеТипов(Массив);

ТаблицаСверки.Колонки.Добавить("МПЗ",ОписаниеТиповСправочников);
ТаблицаСверки.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(9,3)));
ТаблицаСверки.Колонки.Добавить("КоличествоПеремещено",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(9,3)));

Запрос.Текст = 
	"ВЫБРАТЬ
	|	Реализация.Ссылка,
	|	Реализация.Контрагент КАК Контрагент
	|ИЗ
	|	Документ.Реализация КАК Реализация
	|ГДЕ
	|	Реализация.Дата МЕЖДУ &ДатаНач И &ДатаКон
	|	И Реализация.ПометкаУдаления = ЛОЖЬ
	|	И Реализация.Подразделение = &Подразделение
	|	И Реализация.МестоХранения = &МестоХранения";
	Если Не Отчет.Контрагент.Пустая() Тогда
	Запрос.Текст = Запрос.Текст + " И Реализация.Контрагент = &Контрагент";	
	Запрос.УстановитьПараметр("Контрагент",Отчет.Контрагент);
	КонецЕсли;
Запрос.Текст = Запрос.Текст + " ИТОГИ ПО Контрагент"; 
Запрос.УстановитьПараметр("ДатаНач",НачалоДня(Отчет.Период.ДатаНачала));
Запрос.УстановитьПараметр("ДатаКон",КонецДня(Отчет.Период.ДатаОкончания));
Запрос.УстановитьПараметр("Подразделение",Отчет.Подразделение);
Запрос.УстановитьПараметр("МестоХранения",Отчет.МестоХраненияГП);
РезультатЗапроса = Запрос.Выполнить();
ВыборкаКонтрагент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаКонтрагент.Следующий() Цикл
	ОблКонтрагент.Параметры.Контрагент = ВыборкаКонтрагент.Контрагент;
	ТабДок.Вывести(ОблКонтрагент);
	ТабДок.НачатьГруппуСтрок("Контрагент", Истина);
	ВыборкаДетальныеЗаписи = ВыборкаКонтрагент.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Реализация = ВыборкаДетальныеЗаписи.Ссылка;
		ОблРеализация.Параметры.ДокументНаименование = ""+Реализация+?(Реализация.Внутренняя," (Внутренняя)","");
		ОблРеализация.Параметры.Документ = Реализация;
		ОблРеализация.Параметры.Комментарий = Реализация.Комментарий;
		ТабДок.Вывести(ОблРеализация); 
		ТабДок.НачатьГруппуСтрок("Реализация",Реализация.Внутренняя);
		ТаблицаСверки.Очистить();
			Для каждого ТЧ Из Реализация.ТабличнаяЧасть Цикл
			ТЧ_МПЗ = ТаблицаСверки.Добавить();
			ТЧ_МПЗ.МПЗ = ТЧ.Товар;
		    ТЧ_МПЗ.Количество = ПолучитьБазовоеКоличество(ТЧ.Количество,ТЧ.ЕдиницаИзмерения); 		
			КонецЦикла;
		ТаблицаСверки.Свернуть("МПЗ","Количество,КоличествоПеремещено");
			Если Реализация.Внутренняя Тогда
				Если Найти(Реализация.Комментарий,"Передачи") > 0 Тогда
				флПередачи = Истина;
				Иначе	
				флПередачи = Ложь;
				КонецЕсли; 
			ЗапросМХ.Текст = 
				"ВЫБРАТЬ
				|	МестаХраненияОбороты.Регистратор КАК Регистратор,
				|	МестаХраненияОбороты.МПЗ КАК МПЗ,
				|	МестаХраненияОбороты.КоличествоРасход КАК КоличествоРасход
				|ИЗ
				|	РегистрНакопления.МестаХранения.Обороты(&ДатаНач, &ДатаКон, Регистратор, ) КАК МестаХраненияОбороты
				|ГДЕ
				|	МестаХраненияОбороты.МестоХранения = &МестоХранения
				|ИТОГИ
				|	СУММА(КоличествоРасход)
				|ПО
				|	Регистратор,
				|	МПЗ";
			ЗапросМХ.УстановитьПараметр("МестоХранения",Отчет.МестоХраненияГП); 
			ЗапросМХ.УстановитьПараметр("ДатаНач",НачалоДня(Реализация.Дата)); 
			ЗапросМХ.УстановитьПараметр("ДатаКон",КонецДня(Реализация.Дата));
			РезультатЗапросаМХ = ЗапросМХ.Выполнить();
			ВыборкаДокМХ = РезультатЗапросаМХ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			    Пока ВыборкаДокМХ.Следующий() Цикл
					Если ТипЗнч(ВыборкаДокМХ.Регистратор) = Тип("ДокументСсылка.ДвижениеМПЗ") Тогда
						Если флПередачи Тогда
						Продолжить;					
						КонецЕсли; 
							Если ВыборкаДокМХ.Регистратор.МестоХраненияВ.Подразделение <> Реализация.ПодразделениеПолучатель Тогда
							Продолжить;
							КонецЕсли;
					ИначеЕсли ТипЗнч(ВыборкаДокМХ.Регистратор) = Тип("ДокументСсылка.ПередачаНаЛинейку") Тогда 
						Если Не флПередачи Тогда
						Продолжить;					
						КонецЕсли;
							Если ВыборкаДокМХ.Регистратор.МестоХраненияКанбанов.Подразделение <> Реализация.ПодразделениеПолучатель Тогда
							Продолжить;
							КонецЕсли;
					Иначе
					Продолжить;						
					КонецЕсли;
				КоличествоДок = 0;
				ВыборкаДетальныеЗаписиМХ = ВыборкаДокМХ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаДетальныеЗаписиМХ.Следующий() Цикл
					Выборка = ТаблицаСверки.Найти(ВыборкаДетальныеЗаписиМХ.МПЗ,"МПЗ");
						Если Выборка = Неопределено Тогда
						ТЧ_МПЗ = ТаблицаСверки.Добавить();
						ТЧ_МПЗ.МПЗ = ВыборкаДетальныеЗаписиМХ.МПЗ;
				    	ТЧ_МПЗ.КоличествоПеремещено = ВыборкаДетальныеЗаписиМХ.КоличествоРасход;			
						Иначе	
						Выборка.КоличествоПеремещено = Выборка.КоличествоПеремещено + ВыборкаДетальныеЗаписиМХ.КоличествоРасход; 
						КонецЕсли;
					КоличествоДок = КоличествоДок + ВыборкаДетальныеЗаписиМХ.КоличествоРасход; 
					КонецЦикла; 		
				КонецЦикла;				
			КонецЕсли;	
		ТаблицаСверки.Сортировать("МПЗ");
			Для каждого ТЧ Из ТаблицаСверки Цикл	
			ОблМПЗ.Параметры.Наименование = СокрЛП(ТЧ.МПЗ.Наименование);
			ОблМПЗ.Параметры.МПЗ = ТЧ.МПЗ;
			ОблМПЗ.Параметры.ЕдиницаИзмерения = ТЧ.МПЗ.ЕдиницаИзмерения;
			ОблМПЗ.Параметры.Количество = ТЧ.Количество;
			ОблМПЗ.Параметры.КоличествоПеремещено = ТЧ.КоличествоПеремещено;
			СписокПараметров = Новый СписокЗначений;

			СписокПараметров.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
			СписокПараметров.Добавить(ТЧ.МПЗ);
			ОблМПЗ.Параметры.СписокПараметров = СписокПараметров;
			ТекОбл = ТабДок.Вывести(ОблМПЗ);
				Если ТЧ.Количество <> ТЧ.КоличествоПеремещено Тогда
				ТабДок.Область(ТекОбл.Верх, ТекОбл.Лево, ТекОбл.Низ, ТекОбл.Лево).ЦветТекста = Новый Цвет(255,0,0);
				КонецЕсли; 
			КонецЦикла;
		ОблИтогоРеализация.Параметры.КоличествоИтого = ТаблицаСверки.Итог("Количество");
		ОблИтогоРеализация.Параметры.КоличествоПеремещеноИтого = ТаблицаСверки.Итог("КоличествоПеремещено");
		ТабДок.Вывести(ОблИтогоРеализация);
		ТабДок.ЗакончитьГруппуСтрок();	
		КонецЦикла;
	ТабДок.ЗакончитьГруппуСтрок();
	КонецЦикла;
ТабДок.Вывести(ОблКонец);
ТабДок.ФиксацияСверху = 3;
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	Если ЭтаФорма.ПроверитьЗаполнение() Тогда
	СформироватьНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Функция ВывестиРасшифровкуПоДокументам(СЗ)
ТД = Новый ТабличныйДокумент;
Реализация = СЗ[0].Значение;
	Если Не Реализация.Внутренняя Тогда
	Возврат(ТД);
	КонецЕсли;
		Если Найти(Реализация.Комментарий,"Передачи") > 0 Тогда
		флПередачи = Истина;
		Иначе	
		флПередачи = Ложь;
		КонецЕсли;
ЗапросМХ = Новый Запрос;

ОбъектЗн = РеквизитФормыВЗначение("Отчет");
Макет = ОбъектЗн.ПолучитьМакет("Документы");

ОблШапка = Макет.ПолучитьОбласть("Шапка");
ОблПеремещение = Макет.ПолучитьОбласть("Перемещение");
ОблКонец = Макет.ПолучитьОбласть("Конец");

ОблШапка.Параметры.Реализация = Реализация;
ОблШапка.Параметры.МПЗ = СЗ[1].Значение;
ТД.Вывести(ОблШапка);

ЗапросМХ.Текст = 
	"ВЫБРАТЬ
	|	МестаХраненияОбороты.Регистратор КАК Регистратор,
	|	МестаХраненияОбороты.МПЗ КАК МПЗ,
	|	МестаХраненияОбороты.КоличествоРасход КАК КоличествоРасход
	|ИЗ
	|	РегистрНакопления.МестаХранения.Обороты(&ДатаНач, &ДатаКон, Регистратор, ) КАК МестаХраненияОбороты
	|ГДЕ
	|	МестаХраненияОбороты.МестоХранения = &МестоХранения
	|	И МестаХраненияОбороты.МПЗ = &МПЗ
	|ИТОГИ
	|	СУММА(КоличествоРасход)
	|ПО
	|	Регистратор,
	|	МПЗ";
ЗапросМХ.УстановитьПараметр("МестоХранения",Отчет.МестоХраненияГП); 
ЗапросМХ.УстановитьПараметр("ДатаНач",НачалоДня(Реализация.Дата)); 
ЗапросМХ.УстановитьПараметр("ДатаКон",КонецДня(Реализация.Дата));
ЗапросМХ.УстановитьПараметр("МПЗ",СЗ[1].Значение);
РезультатЗапросаМХ = ЗапросМХ.Выполнить();
ВыборкаДокМХ = РезультатЗапросаМХ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
    Пока ВыборкаДокМХ.Следующий() Цикл
		Если ТипЗнч(ВыборкаДокМХ.Регистратор) = Тип("ДокументСсылка.ДвижениеМПЗ") Тогда
			Если флПередачи Тогда
			Продолжить;					
			КонецЕсли; 
				Если ВыборкаДокМХ.Регистратор.МестоХраненияВ.Подразделение <> Реализация.ПодразделениеПолучатель Тогда
				Продолжить;
				КонецЕсли; 
		ИначеЕсли ТипЗнч(ВыборкаДокМХ.Регистратор) = Тип("ДокументСсылка.ПередачаНаЛинейку") Тогда 
			Если Не флПередачи Тогда
			Продолжить;					
			КонецЕсли;
		Иначе
		Продолжить;
		КонецЕсли;
	КоличествоДок = 0;
	ВыборкаДетальныеЗаписиМХ = ВыборкаДокМХ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДетальныеЗаписиМХ.Следующий() Цикл
		КоличествоДок = ВыборкаДетальныеЗаписиМХ.КоличествоРасход; 
		КонецЦикла;
	ОблПеремещение.Параметры.Документ = ВыборкаДокМХ.Регистратор;
	ОблПеремещение.Параметры.ЕдиницаИзмерения = СЗ[1].Значение.ЕдиницаИзмерения;
	ОблПеремещение.Параметры.Количество = КоличествоДок;
	ТД.Вывести(ОблПеремещение); 		
	КонецЦикла;
ТД.Вывести(ОблКонец);
ТД.Защита = Истина;
ТД.ФиксацияСверху = 3; 
Возврат(ТД);
КонецФункции

&НаКлиенте
Процедура ТабДокОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	Если ТипЗнч(Расшифровка) = Тип("СписокЗначений") Тогда
	СтандартнаяОбработка = Ложь;
	ТД = ВывестиРасшифровкуПоДокументам(Расшифровка);
	ТД.Показать("Перемещения со склада ГП по реализации");
	КонецЕсли;
КонецПроцедуры
