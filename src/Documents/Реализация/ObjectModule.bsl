
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)Экспорт
Автор = ПараметрыСеанса.Пользователь;
Коэфф = 1;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаявкаОтПокупателя") Тогда
	ДокументОснование = ДанныеЗаполнения.Ссылка;
	Подразделение = ДанныеЗаполнения.Подразделение;
	Контрагент = ДанныеЗаполнения.Контрагент;
	Договор = ДанныеЗаполнения.Договор;
	Коэфф = ДанныеЗаполнения.Коэфф;
	Запрос = Новый Запрос;

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаявкиОтПокупателейОстатки.МПЗ КАК МПЗ,
		|	ЗаявкиОтПокупателейОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ЗаявкиОтПокупателей.Остатки(&НаДату, ) КАК ЗаявкиОтПокупателейОстатки
		|ГДЕ
		|	ЗаявкиОтПокупателейОстатки.ЗаявкаОтПокупателя = &ЗаявкаОтПокупателя";
	Запрос.УстановитьПараметр("ЗаявкаОтПокупателя", ДанныеЗаполнения.Ссылка);
	Запрос.УстановитьПараметр("НаДату", ТекущаяДата());
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();	
		Для Каждого ТекСтрокаТабличнаяЧасть Из ДанныеЗаполнения.ТабличнаяЧасть Цикл	
		ВыборкаДетальныеЗаписи.Сбросить();
			Если ВыборкаДетальныеЗаписи.НайтиСледующий(Новый Структура("МПЗ",ТекСтрокаТабличнаяЧасть.МПЗ)) Тогда
			НоваяСтрока = ТабличнаяЧасть.Добавить();
			НоваяСтрока.ВидТовара = ТекСтрокаТабличнаяЧасть.ВидМПЗ;
			НоваяСтрока.Товар = ТекСтрокаТабличнаяЧасть.МПЗ;
			НоваяСтрока.Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток/ТекСтрокаТабличнаяЧасть.ЕдиницаИзмерения.Коэффициент;
			НоваяСтрока.ЕдиницаИзмерения = ТекСтрокаТабличнаяЧасть.ЕдиницаИзмерения; 
			//НоваяСтрока.Цена = ОбщийМодульВызовСервера.ПолучитьСтоимостьМПЗ(ТекСтрокаТабличнаяЧасть.МПЗ,1,ТекущаяДата())*ТекСтрокаТабличнаяЧасть.ЕдиницаИзмерения.Коэффициент*?(Коэфф > 0,Коэфф,1);
			НоваяСтрока.Цена = ТекСтрокаТабличнаяЧасть.Цена;
			НоваяСтрока.Сумма = НоваяСтрока.Цена*НоваяСтрока.Количество;
			НоваяСтрока.СтавкаНДС = ТекСтрокаТабличнаяЧасть.СтавкаНДС;
				Если Не Договор.БезНДС Тогда
				НоваяСтрока.СтавкаНДС = ТекСтрокаТабличнаяЧасть.СтавкаНДС;
				НоваяСтрока.НДС = НоваяСтрока.Сумма*НоваяСтрока.СтавкаНДС.Ставка/100;
				НоваяСтрока.Всего = НоваяСтрока.Сумма + НоваяСтрока.НДС;				
				Иначе
				НоваяСтрока.Всего = НоваяСтрока.Сумма; 	
				КонецЕсли; 
			КонецЕсли;  
		КонецЦикла;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеМПЗ") Тогда
	ДокументОснование = ДанныеЗаполнения.Ссылка;
	Подразделение = ДанныеЗаполнения.Подразделение;
	Контрагент = ДанныеЗаполнения.Контрагент;
	Договор = ДанныеЗаполнения.Договор;
		Для Каждого ТекСтрокаТабличнаяЧасть Из ДанныеЗаполнения.ТабличнаяЧасть Цикл
		НоваяСтрока = ТабличнаяЧасть.Добавить();
		НоваяСтрока.ВидТовара = ТекСтрокаТабличнаяЧасть.ВидМПЗ;
		НоваяСтрока.Товар = ТекСтрокаТабличнаяЧасть.МПЗ;
		НоваяСтрока.Количество = ТекСтрокаТабличнаяЧасть.Количество;
		НоваяСтрока.ЕдиницаИзмерения = ТекСтрокаТабличнаяЧасть.ЕдиницаИзмерения;
		НоваяСтрока.Цена = ТекСтрокаТабличнаяЧасть.Цена;
		НоваяСтрока.Сумма = ТекСтрокаТабличнаяЧасть.Сумма;
		НоваяСтрока.СтавкаНДС = ТекСтрокаТабличнаяЧасть.СтавкаНДС;
		НоваяСтрока.НДС = ТекСтрокаТабличнаяЧасть.НДС;
		НоваяСтрока.Всего = ТекСтрокаТабличнаяЧасть.Всего;
		КонецЦикла;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ДвижениеМПЗ") Тогда
	ДокументОснование = ДанныеЗаполнения.Ссылка;
	Подразделение = ДанныеЗаполнения.Подразделение;
		Для Каждого ТекСтрокаТабличнаяЧасть Из ДанныеЗаполнения.ТабличнаяЧасть Цикл
		НоваяСтрока = ТабличнаяЧасть.Добавить();
		НоваяСтрока.ВидТовара = ТекСтрокаТабличнаяЧасть.ВидМПЗ;
		НоваяСтрока.Товар = ТекСтрокаТабличнаяЧасть.МПЗ;
		НоваяСтрока.Количество = ТекСтрокаТабличнаяЧасть.Количество;
		НоваяСтрока.ЕдиницаИзмерения = ТекСтрокаТабличнаяЧасть.ЕдиницаИзмерения;
		Цены = РегистрыСведений.Цены.ПолучитьПоследнее(ТекущаяДата(),Новый Структура("МПЗ",ТекСтрокаТабличнаяЧасть.МПЗ));
		НоваяСтрока.Цена = Цены.Цена*ТекСтрокаТабличнаяЧасть.ЕдиницаИзмерения.Коэффициент;
		НоваяСтрока.Сумма = НоваяСтрока.Цена*НоваяСтрока.Количество;
		НоваяСтрока.СтавкаНДС = Константы.ОсновнаяСтавкаНДС.Получить();
		НоваяСтрока.НДС = НоваяСтрока.Сумма*НоваяСтрока.СтавкаНДС.Ставка/100;
		НоваяСтрока.Всего = НоваяСтрока.Сумма + НоваяСтрока.НДС;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если Номер = "" Тогда
	УстановитьНовыйНомер(ПрисвоитьПрефикс(Подразделение,Дата));
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если Внутренняя Тогда
	Сообщить("Внутренняя реализация не подлежит проведению!");
	Отказ = Истина;
	Возврат;	
	КонецЕсли; 
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаявкаОтПокупателя") Тогда
		ЗаявкиОтПокупателей = РегистрыНакопления.ЗаявкиОтПокупателей;
		Движения.ЗаявкиОтПокупателей.Записывать = Истина;
		ВиртуальнаяТабличнаяЧасть = ТабличнаяЧасть.Выгрузить();
		ВиртуальнаяТабличнаяЧасть.Свернуть("ВидТовара,Товар,ЕдиницаИзмерения","Количество");
			Для Каждого ТЧ Из ВиртуальнаяТабличнаяЧасть Цикл
			Фильтр   = Новый Структура;
			Фильтр.Вставить("МПЗ", ТЧ.Товар);
			Фильтр.Вставить("Контрагент",Контрагент);
			Фильтр.Вставить("Договор", Договор);
			Фильтр.Вставить("ДатаИсполнения", ДокументОснование.ДатаИсполнения);
			Фильтр.Вставить("ЗаявкаОтПокупателя", ДокументОснование);
			Остаток = ЗаявкиОтПокупателей.Остатки(Дата,Фильтр,"МПЗ,Контрагент,Договор,ДатаИсполнения,ЗаявкаОтПокупателя", "Количество");
			БазовоеКоличество = ПолучитьБазовоеКоличество(ТЧ.Количество,ТЧ.ЕдиницаИзмерения);
				Если Остаток.Количество() > 0 Тогда
				КолОстаток = Остаток[0].Количество;
				Иначе
				КолОстаток = 0;
				КонецЕсли; 
					Если КолОстаток >= БазовоеКоличество Тогда
					Движение = Движения.ЗаявкиОтПокупателей.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
					Движение.Период = Дата;
					Движение.МПЗ = ТЧ.Товар;
					Движение.Контрагент = Контрагент;
					Движение.Договор = Договор;
					Движение.ДатаИсполнения = ДокументОснование.ДатаИсполнения;
					Движение.ЗаявкаОтПокупателя = ДокументОснование;
					Движение.Количество = БазовоеКоличество;
					Иначе
					Отказ = Истина;
					Сообщить("Товар "+ТЧ.Товар+" кол-во в реализации: "+БазовоеКоличество+" требуется по заявке: "+КолОстаток);
					КонецЕсли; 		
			КонецЦикла;
		ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказНаПроизводство") Тогда
		// регистр Долги Расход
		Движения.Долги.Записывать = Истина;
		ВиртуальнаяТабличнаяЧасть = ТабличнаяЧасть.Выгрузить();
		ВиртуальнаяТабличнаяЧасть.Свернуть("ВидТовара,Товар,ЕдиницаИзмерения","Количество");
		КоличествоВсего = 0;
			Для Каждого ТЧ Из ВиртуальнаяТабличнаяЧасть Цикл
			БазовоеКоличество = ПолучитьБазовоеКоличество(ТЧ.Количество,ТЧ.ЕдиницаИзмерения);
			КоличествоВсего = КоличествоВсего + БазовоеКоличество;
			КолОстаток = ОбщийМодульРаботаСРегистрами.ПолучитьОстатокПоДолгам(ДокументОснование,ТЧ.Товар,Дата);
				Если КолОстаток >= БазовоеКоличество Тогда
				Движение = Движения.Долги.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Документ = ДокументОснование;
				Движение.Продукция = ТЧ.Товар;
				Движение.Количество = БазовоеКоличество;
				Иначе
				Отказ = Истина;
				Сообщить("Продукция: "+СокрЛП(ТЧ.Товар.Наименование)+" требуется реализовать: "+БазовоеКоличество+" недостаёт в регистре долгов: "+Строка(БазовоеКоличество-КолОстаток));
				КонецЕсли;
			КонецЦикла;
		ОбщийМодульВызовСервера.ПроверитьЗаказНаПроизводство(ДокументОснование,КоличествоВсего,Дата);
		КонецЕсли; 
	КонецЕсли;
// регистр МестаХранения Расход
Движения.МестаХранения.Записывать = Истина;
ВиртуальнаяТабличнаяЧасть = ТабличнаяЧасть.Выгрузить();
ВиртуальнаяТабличнаяЧасть.Свернуть("ВидТовара,Товар,ЕдиницаИзмерения","Количество");
	Для Каждого ТЧ Из ВиртуальнаяТабличнаяЧасть Цикл
	БазовоеКоличество = ПолучитьБазовоеКоличество(ТЧ.Количество,ТЧ.ЕдиницаИзмерения);
	КолОстаток = ОбщийМодульРаботаСРегистрами.ПолучитьОстатокПоМестуХранения(МестоХранения,ТЧ.Товар,Дата);
		Если КолОстаток >= БазовоеКоличество Тогда
		Движение = Движения.МестаХранения.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.МестоХранения = МестоХранения;
		Движение.ВидМПЗ = ТЧ.ВидТовара;
		Движение.МПЗ = ТЧ.Товар;
		Движение.Количество = БазовоеКоличество;
		Иначе
		Отказ = Истина;
		Сообщить("Товар: "+СокрЛП(ТЧ.Товар.Наименование)+" требуется списать: "+БазовоеКоличество+" недостаёт на складе: "+Строка(БазовоеКоличество-КолОстаток));
		КонецЕсли;
	КонецЦикла;
		Если Не НеУчитыватьГТД Тогда
		Запрос = Новый Запрос;
		// регистр ГТД Расход
		Движения.ГТД.Записывать = Истина;
			Для Каждого ТЧ Из ТабличнаяЧасть Цикл
			КоличествоОстаток = ПолучитьБазовоеКоличество(ТЧ.Количество,ТЧ.ЕдиницаИзмерения);
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	ГТДОстатки.НомерГТД КАК НомерГТД,
				|	ГТДОстатки.КоличествоОстаток КАК КоличествоОстаток
				|ИЗ
				|	РегистрНакопления.ГТД.Остатки(&НаДату, Товар = &Товар) КАК ГТДОстатки
				|
				|УПОРЯДОЧИТЬ ПО
				|	ГТДОстатки.НомерГТД.ДатаВнесения";
			Запрос.УстановитьПараметр("НаДату", Дата);
			Запрос.УстановитьПараметр("Товар", ТЧ.Товар);
			РезультатЗапроса = Запрос.Выполнить();
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					Если КоличествоОстаток > 0 Тогда
					Движение = Движения.ГТД.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
					Движение.Период = Дата;
					Движение.Товар = ТЧ.Товар;
					Движение.НомерГТД = ВыборкаДетальныеЗаписи.НомерГТД;
						Если ВыборкаДетальныеЗаписи.КоличествоОстаток <= КоличествоОстаток Тогда
						Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток;
						КоличествоОстаток = КоличествоОстаток - ВыборкаДетальныеЗаписи.КоличествоОстаток;
						Иначе
						Движение.Количество = КоличествоОстаток;
						КоличествоОстаток = 0;
						КонецЕсли;
					КонецЕсли; 
				КонецЦикла; 
			КонецЦикла;
		КонецЕсли;
КонецПроцедуры
