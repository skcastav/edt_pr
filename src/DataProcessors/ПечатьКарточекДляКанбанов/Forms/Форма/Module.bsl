
&НаСервере
Процедура ПечатьКарточекУЛИШИНаСервере(КоличествоКопийЗадано)
ТабДок.Очистить();
barcode = ПолучитьCOMОбъект("","STROKESCRIBE.StrokeScribeClass.1");
barcode.Alphabet=25; //QRCODE
Макет = ПолучитьОбщийМакет("QRКодКанбанУЛИШИ");
ОблQRКод = Макет.ПолучитьОбласть("QRКод|Секция");
МаксКолНаЛисте = 5;
Четный = Ложь;
	Для каждого ТЧ Из Объект.ТаблицаКанбанов Цикл
		Если ТЧ.Пометка Тогда
			Если КоличествоКопийЗадано > 0 Тогда
			КоличествоКопий = КоличествоКопийЗадано;
			Иначе
			КоличествоКопий = ТЧ.КоличествоЯчеек;
			КонецЕсли; 
				Для к = 1 По КоличествоКопий Цикл
				QRCode = ЗначениеВСтрокуВнутр(ТЧ.МестоХраненияПотребитель)+";"+ЗначениеВСтрокуВнутр(ТЧ.Канбан)+";"+ТЧ.Количество;
				ИмяФайла=ПолучитьИмяВременногоФайла("wmf");
				barcode.Text=QRCode;
				barcode.UTF8=1;
				barcode.QrECL=2;
				код=barcode.SavePicture(ИмяФайла,7,100,100);          
					Если код <> 0 Тогда //Проверка результата генерации штрихкода
					Сообщить(строка(код) + " - " + barcode.ErrorDescription);
					Возврат;
					КонецЕсли;
				рис=ОблQRКод.Рисунки.QRCode;
				рис.РазмерКартинки=РазмерКартинки.Пропорционально;
				рис.Линия = Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии);
				рис.Картинка = Новый Картинка(ИмяФайла);
				НомерДетали = СокрЛП(Лев(ТЧ.Канбан.Наименование,Найти(ТЧ.Канбан.Наименование,"(")-1));
					Пока Найти(НомерДетали,".") > 1 Цикл
					НомерДетали = Сред(НомерДетали,Найти(НомерДетали,".")+1);	
					КонецЦикла;
				ОблQRКод.Параметры.НомерДетали = НомерДетали;
				ОблQRКод.Параметры.Наименование = СокрЛП(ТЧ.Канбан.Наименование);
				ОблQRКод.Параметры.Количество = ТЧ.Количество;
				ПараметрыИзделийУЛИШИ = РегистрыСведений.ПараметрыИзделийУЛИШИ.Выбрать(Новый Структура("Номенклатура",ТЧ.Канбан));
					Пока ПараметрыИзделийУЛИШИ.Следующий() Цикл
					ОблQRКод.Параметры.ВидТары = ПараметрыИзделийУЛИШИ.ВидТары;
					НомерБО = ПараметрыИзделийУЛИШИ.БазоваяОтливка;
						Пока Найти(НомерБО,".") > 1 Цикл
						НомерБО = Сред(НомерБО,Найти(НомерБО,".")+1);	
						КонецЦикла;
					ОблQRКод.Параметры.НомерБО = НомерБО;
					ОблQRКод.Параметры.Прессформа = ПараметрыИзделийУЛИШИ.Прессформа;				
					КонецЦикла; 
						Если Не Четный Тогда
						ТабДок.Вывести(ОблQRКод);
						Четный = Истина;
						Иначе	
						ТабДок.Присоединить(ОблQRКод);
						Четный = Ложь;
						МаксКолНаЛисте = МаксКолНаЛисте - 1;
						КонецЕсли; 
							Если МаксКолНаЛисте = 0 Тогда
							ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
							МаксКолНаЛисте = 5;				
							КонецЕсли;  
				УдалитьФайлы(ИмяФайла);
				КонецЦикла;
		КонецЕсли; 
	КонецЦикла; 
ТабДок.РазмерСтраницы = "A4";
ТабДок.ПолеСлева = 0;
ТабДок.ПолеСверху = 0;
ТабДок.ПолеСнизу = 0;
ТабДок.ПолеСправа = 0;
ТабДок.РазмерКолонтитулаСверху = 0;
ТабДок.РазмерКолонтитулаСнизу = 0;
КонецПроцедуры

&НаКлиенте
Процедура ПечатьКарточекУЛИШИ(Команда)
КоличествоКопий = 0;
	Если ВвестиЧисло(КоличествоКопий,"Введите кол-во копий (если 0, то по кол-ву ячеек из таблицы)",1,0) Тогда
	ПечатьКарточекУЛИШИНаСервере(КоличествоКопий);
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ДобавитьИзГруппыНаСервере(ВыбГруппа)
Запрос = Новый Запрос;

Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ЭтоГруппа = ЛОЖЬ
	|	И Номенклатура.Ссылка В ИЕРАРХИИ(&ГруппаНоменклатуры)
	|	И Номенклатура.Канбан <> &ПустойКанбан"; 
Запрос.УстановитьПараметр("ГруппаНоменклатуры", ВыбГруппа);
Запрос.УстановитьПараметр("ПустойКанбан", Справочники.ВидыКанбанов.ПустаяСсылка());
РезультатЗапроса = Запрос.Выполнить();
ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
	ТЧ = Объект.ТаблицаКанбанов.Добавить();
	ТЧ.Канбан = ВыборкаДетальныеЗаписи.Ссылка;
	ТЧ.КоличествоЯчеек = ВыборкаДетальныеЗаписи.Ссылка.КолКанбан;
	ТЧ.Количество = ВыборкаДетальныеЗаписи.Ссылка.КолВКанбане;
	КонецЦикла;
Объект.ТаблицаКанбанов.Сортировать("Канбан");
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзГруппы(Команда)
	Если Объект.ТаблицаКанбанов.Количество() > 0 Тогда
		Если Вопрос("Таблица канбанов не пустая! Очистить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		Объект.ТаблицаКанбанов.Очистить();
		КонецЕсли;
	КонецЕсли; 
ВыбГруппа = Неопределено;
Результат = ОткрытьФормуМодально("Справочник.Номенклатура.ФормаВыбораГруппы");
	Если Результат <> Неопределено Тогда
	ДобавитьИзГруппыНаСервере(Результат);
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ДобавитьПоВидамКанбановНаСервере()
Запрос = Новый Запрос;

Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Канбан В ИЕРАРХИИ(&СписокВидовКанбанов)";
Запрос.УстановитьПараметр("СписокВидовКанбанов", СписокВидовКанбанов);
РезультатЗапроса = Запрос.Выполнить();
ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
	ТЧ = Объект.ТаблицаКанбанов.Добавить();
	ТЧ.Канбан = ВыборкаДетальныеЗаписи.Ссылка;
	ТЧ.КоличествоЯчеек = ВыборкаДетальныеЗаписи.Ссылка.КолКанбан;
	ТЧ.Количество = ВыборкаДетальныеЗаписи.Ссылка.КолВКанбане;
	КонецЦикла;
Объект.ТаблицаКанбанов.Сортировать("Канбан");
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоВидамКанбанов(Команда)
	Если Объект.ТаблицаКанбанов.Количество() > 0 Тогда
		Если Вопрос("Таблица канбанов не пустая! Очистить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		Объект.ТаблицаКанбанов.Очистить();
		КонецЕсли;
	КонецЕсли;
ДобавитьПоВидамКанбановНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	Для каждого ТЧ Из Объект.ТаблицаКанбанов Цикл		
	ТЧ.Пометка = Истина;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВсе(Команда)
	Для каждого ТЧ Из Объект.ТаблицаКанбанов Цикл		
	ТЧ.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура НазначитьВыбраннымНаСервере()
	Для каждого ТЧ Из Объект.ТаблицаКанбанов Цикл
		Если ТЧ.Пометка Тогда
		ТЧ.МестоХраненияПотребитель = Объект.МестоХраненияПотребитель;
		КонецЕсли; 
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьВыбранным(Команда)
НазначитьВыбраннымНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокуНоменклатуры(Наименование,МестоХранения,Количество)
Спецификация = Справочники.Номенклатура.НайтиПоНаименованию(Наименование,Истина);
	Если Не Спецификация.Пустая() Тогда
	ТЧ = Объект.ТаблицаКанбанов.Добавить();
	ТЧ.Пометка = Истина;
	ТЧ.МестоХраненияПотребитель = МестоХранения;
	ТЧ.Канбан = Спецификация;
	ТЧ.КоличествоЯчеек = Спецификация.КолКанбан;	
	ТЧ.Количество = Количество;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьМестоХранения(Наименование)
Возврат(Справочники.МестаХранения.НайтиПоНаименованию(Наименование,Истина));
КонецФункции

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
Результат = ОбщийМодульКлиент.ОткрытьФайлExcel("Выберите файл c данными по канбанам минитипографии");
	Если Результат <> Неопределено Тогда
	ExcelЛист = Результат.ExcelЛист;
	КолСтрок = Результат.КоличествоСтрок;
	    Для к = 2 по КолСтрок Цикл
		Состояние("Обработка...",к*100/КолСтрок,"Загрузка данными по канбанам минитипографии из файла..."); 
		Наименование = СокрЛП(ExcelЛист.Cells(к,1).Value);
		МестоХранения = ПолучитьМестоХранения(СокрЛП(ExcelЛист.Cells(к,3).Value));
			Если Не МестоХранения.Пустая() Тогда
			ДобавитьСтрокуНоменклатуры(Наименование,МестоХранения,Число(ExcelЛист.Cells(к,2).Value));
			Иначе
			Сообщить("Место хранения "+СокрЛП(ExcelЛист.Cells(к, 2).Value)+" не найдено!");
			КонецЕсли;
	    КонецЦикла;
	Результат.Excel.Quit();
	КонецЕсли;
КонецПроцедуры
