
&НаСервере
Функция СоздатьЗаявкуОтПокупателя(НомерЗаказа,ДатаДокумента,Дополнение,Комментарий)
	Попытка
	ЗаявкаОтПокупателя = Документы.ЗаявкаОтПокупателя.СоздатьДокумент();
	ЗаявкаОтПокупателя.Номер = НомерЗаказа;
	//ЗаявкаОтПокупателя.Подразделение = Подразделение;
	ЗаявкаОтПокупателя.Автор = ПараметрыСеанса.Пользователь;
	ЗаявкаОтПокупателя.Дата = ДатаДокумента;
	//ЗаявкаОтПокупателя.ДатаИсполнения = ДатаИсполнения;
	ЗаявкаОтПокупателя.Контрагент = Контрагент;
	ЗаявкаОтПокупателя.Договор = Контрагент.ОсновнойДоговор;
	ЗаявкаОтПокупателя.Курс = 1;
	ЗаявкаОтПокупателя.Коэфф = 1;
	ЗаявкаОтПокупателя.Дополнение = Дополнение;
	ЗаявкаОтПокупателя.Комментарий = Комментарий;
		Для каждого ТЧ_МПЗ Из ТаблицаМПЗ Цикл
		ТЧ = ЗаявкаОтПокупателя.ТабличнаяЧасть.Добавить();
			Если ТипЗнч(ТЧ_МПЗ.МПЗ) = Тип("СправочникСсылка.Материалы") Тогда
			ТЧ.ВидМПЗ = Перечисления.ВидыМПЗ.Материалы;
			Иначе	
			ТЧ.ВидМПЗ = Перечисления.ВидыМПЗ.Полуфабрикаты;
			КонецЕсли;		
		ТЧ.МПЗ = ТЧ_МПЗ.МПЗ;
		ТЧ.Количество = ТЧ_МПЗ.Количество;
		ТЧ.ЕдиницаИзмерения = ТЧ_МПЗ.ЕдиницаИзмерения;
		ТЧ.Цена = ОбщийМодульВызовСервера.ПолучитьПоследнююЦену(ТЧ_МПЗ.МПЗ,ТекущаяДата())*ТЧ_МПЗ.ЕдиницаИзмерения.Коэффициент;
			Если ТЧ.Цена = 0 Тогда
			ТЧ.Цена = 1;			
			КонецЕсли; 
		ТЧ.Сумма = ТЧ.Цена*ТЧ.Количество;
			Если Контрагент.ОсновнойДоговор.БезНДС Тогда
			ТЧ.Всего = ТЧ.Сумма;
			Иначе	
			ТЧ.СтавкаНДС = Константы.ОсновнаяСтавкаНДС;
			ТЧ.НДС = ТЧ.Сумма*18/100;				
			ТЧ.Всего = ТЧ.Сумма +  ТЧ.НДС;			
			КонецЕсли; 
		КонецЦикла; 
			Если ЗаявкаОтПокупателя.ТабличнаяЧасть.Количество() > 0 Тогда
			ЗаявкаОтПокупателя.Записать(РежимЗаписиДокумента.Запись);
			Иначе
			Сообщить("Табличная часть документа пустая! Заявка от покупателя "+НомерЗаказа+" не создана!");
			Возврат(Документы.ЗаявкаОтПокупателя.ПустаяСсылка());
			КонецЕсли;
	Исключение
	Сообщить(ОписаниеОшибки());
	Возврат(Документы.ЗаявкаОтПокупателя.ПустаяСсылка());
	КонецПопытки;
Возврат(ЗаявкаОтПокупателя.Ссылка);
КонецФункции 

&НаСервере
Процедура ПроверитьНаСервере()
ТаблицаЗаказов.Очистить();
БазаХарькова = ОбщийМодульСинхронизации.УстановитьCOMСоединение(Объект.ОбменДанными);
	Если БазаХарькова = Неопределено Тогда
	Сообщить("Не открыто соединение с производственной базой Харькова!");
	Возврат;
	КонецЕсли;
Выборка = БазаХарькова.Документы.ЗаказПоставщику.Выбрать(Объект.Период.ДатаНачала,Объект.Период.ДатаОкончания);
	Пока Выборка.Следующий() Цикл
		Если Выборка.Проведен Тогда
			Если СписокПодразделений.НайтиПоЗначению(СокрЛП(Выборка.Подразделение.Наименование)) <> Неопределено Тогда
			ТЧ = ТаблицаЗаказов.Добавить();
			ТЧ.НомерЗаказа = Выборка.Номер;
			ТЧ.ДатаЗаказа = Выборка.Дата;
			ТЧ.Дополнение = Выборка.Дополнение; 
			ТЧ.Комментарий = Выборка.Комментарий;
			ТЧ.Ссылка = Документы.ЗаявкаОтПокупателя.НайтиПоНомеру(Выборка.Номер,Выборка.Дата);
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Соединиться(Команда) 
Состояние("Обработка...",,"Проверка заказов поставщику...");
ПроверитьНаСервере();
КонецПроцедуры

&НаСервере
Функция ПолучитьЕдиницуИзмерения(МПЗ,ЕдиницаИзмерения)
Запрос = Новый Запрос;

Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеЕдиницыИзмерений.Ссылка
	|ИЗ
	|	Справочник.ОсновныеЕдиницыИзмерений КАК ОсновныеЕдиницыИзмерений
	|ГДЕ	
	|	ОсновныеЕдиницыИзмерений.Владелец = &Владелец
	|	И ОсновныеЕдиницыИзмерений.ЕдиницаИзмерения.Наименование = &Наименование";
Запрос.УстановитьПараметр("Владелец", МПЗ);
Запрос.УстановитьПараметр("Наименование", СокрЛП(ЕдиницаИзмерения));
РезультатЗапроса = Запрос.Выполнить();
ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
	Возврат(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
Возврат(Справочники.ОсновныеЕдиницыИзмерений.ПустаяСсылка());
КонецФункции

&НаСервере
Функция ДобавитьМПЗ(ВидМПЗ,Наименование,Количество,ЕдиницаИзмерения)
	Если ВидМПЗ = 0 Тогда
	МПЗ = Справочники.Материалы.НайтиПоНаименованию(Наименование,Истина);
	Иначе	
	МПЗ = Справочники.Номенклатура.НайтиПоНаименованию(Наименование,Истина);
	КонецЕсли;
		Если Не МПЗ.Пустая() Тогда
		ТЧ = ТаблицаМПЗ.Добавить();
		ТЧ.МПЗ = МПЗ;
		ТЧ.Количество = Количество;	
		ТЧ.ЕдиницаИзмерения = ПолучитьЕдиницуИзмерения(МПЗ,ЕдиницаИзмерения);
		Возврат(Истина);
		Иначе
		Сообщить(Наименование+" - не найден в справочнике!");  
		Возврат(Ложь);
		КонецЕсли;
КонецФункции

&НаСервере
Процедура ПеренестиНаСервере()
БазаХарькова = ОбщийМодульСинхронизации.УстановитьCOMСоединение(Объект.ОбменДанными);
	Если БазаХарькова = Неопределено Тогда
	Сообщить("Не открыто соединение с производственной базой Харькова!");
	Возврат;
	КонецЕсли;	
		Для каждого ТЧ Из ТаблицаЗаказов Цикл
			Если ТЧ.Пометка Тогда
				Если Не ТЧ.Ссылка.Пустая() Тогда
				Продолжить;
				КонецЕсли;
			Док = БазаХарькова.Документы.ЗаказПоставщику.НайтиПоНомеру(ТЧ.НомерЗаказа,ТЧ.ДатаЗаказа);
				Если Док <> БазаХарькова.Документы.ЗаказПоставщику.ПустаяСсылка() Тогда
				ТаблицаМПЗ.Очистить();
				флНеНайден = Ложь;
					Для каждого ТЧ_МПЗ Из Док.ТабличнаяЧасть Цикл	
					ВидМПЗ = БазаХарькова.Перечисления.ВидыМПЗ.Индекс(ТЧ_МПЗ.ВидМПЗ);
						Если Не ДобавитьМПЗ(ВидМПЗ,СокрЛП(ТЧ_МПЗ.МПЗ.Наименование),ТЧ_МПЗ.Количество,ТЧ_МПЗ.ЕдиницаИзмерения.Наименование) Тогда	
						флНеНайден = Истина;
						КонецЕсли;				
					КонецЦикла; 
						Если Не флНеНайден Тогда
						ТЧ.Ссылка = СоздатьЗаявкуОтПокупателя(ТЧ.НомерЗаказа,ТЧ.ДатаЗаказа,ТЧ.Дополнение,ТЧ.Комментарий);
						Иначе
						Сообщить(ТЧ.НомерЗаказа+" - документ не перенесён!");			
						КонецЕсли;			
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Перенести(Команда)
Состояние("Обработка...",,"Перенос выбранных заказов поставщику...");
ПеренестиНаСервере(); 
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	Для каждого ТЧ Из ТаблицаЗаказов Цикл	
	ТЧ.Пометка = Истина;
	КонецЦикла; 	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВсе(Команда)
	Для каждого ТЧ Из ТаблицаЗаказов Цикл	
	ТЧ.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры
