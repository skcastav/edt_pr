
&НаСервере
Процедура ЗагрузитьИзФайлаНаСервере(Наименование)
Номен = Справочники.Номенклатура.НайтиПоНаименованию(Наименование,Истина);
	Если Не Номен.Пустая() Тогда
	ТЧ = ТаблицаНоменклатуры.Добавить();
	ТЧ.Пометка = Истина;
	ТЧ.Статус = ПолучитьСтатус(Номен,ТекущаяДата());
	ТЧ.Номенклатура = Номен;
	ТЧ.Товар = Номен.Товар;
	ТЧ.ЗапрещеноКОтгрузке = ТЧ.Номенклатура.ЗапрещеноКОтгрузке;
	Иначе	
	Сообщить(Наименование+" - спецификация не найдена!");
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
ТаблицаНоменклатуры.Очистить();
Результат = ОбщийМодульКлиент.ОткрытьФайлExcel("Выберите файл cо спецификациями");
	Если Результат <> Неопределено Тогда
	ExcelЛист = Результат.ExcelЛист;
	КолСтрок = Результат.КоличествоСтрок;
	    Для к = 2 по КолСтрок Цикл
		Состояние("Обработка...",к*100/КолСтрок,"Загрузка списка номенклатуры...");
  		ЗагрузитьИзФайлаНаСервере(СокрЛП(ExcelЛист.Cells(к,1).Value));
		КонецЦикла;
	Результат.Excel.Quit();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДобавитьПоПрименяемостиНаСервере(ВыбЭлемент)
Запрос = Новый Запрос;

Запрос.Текст = 
	"ВЫБРАТЬ
	|	НормыРасходовСрезПоследних.Владелец КАК Владелец
	|ИЗ
	|	РегистрСведений.НормыРасходов.СрезПоследних(&НаДату, Элемент = &Элемент) КАК НормыРасходовСрезПоследних
	|ГДЕ
	|	НормыРасходовСрезПоследних.Норма > 0
	|	И НормыРасходовСрезПоследних.НормаРасходов.ПометкаУдаления = ЛОЖЬ";
Запрос.УстановитьПараметр("НаДату", ТекущаяДата());
Запрос.УстановитьПараметр("Элемент", ВыбЭлемент);
Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ДобавитьПоПрименяемостиНаСервере(ВыборкаДетальныеЗаписи.Владелец);
		КонецЦикла;
	Иначе
		Если ТипЗнч(ВыбЭлемент) = Тип("СправочникСсылка.Номенклатура") Тогда
		Выборка = ТаблицаНоменклатуры.НайтиСтроки(Новый Структура("Номенклатура",ВыбЭлемент));
			Если Выборка.Количество() = 0 Тогда
			ТЧ = ТаблицаНоменклатуры.Добавить();
			ТЧ.Статус = ПолучитьСтатус(ВыбЭлемент,ТекущаяДата());
			ТЧ.Номенклатура = ВыбЭлемент;
			ТЧ.Товар = ВыбЭлемент.Товар;
			ТЧ.ЗапрещеноКОтгрузке = ВыбЭлемент.ЗапрещеноКОтгрузке;
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДобавитьПоПрименяемостиАналогомНаСервере(ВыбЭлемент)
Запрос = Новый Запрос;

Запрос.Текст = 
	"ВЫБРАТЬ
	|	АналогиНормРасходов.Владелец.Владелец КАК ВладелецВладелец
	|ИЗ
	|	РегистрСведений.АналогиНормРасходов.СрезПоследних(&НаДату, ) КАК АналогиНормРасходовСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.АналогиНормРасходов КАК АналогиНормРасходов
	|		ПО АналогиНормРасходовСрезПоследних.АналогНормыРасходов = АналогиНормРасходов.Ссылка
	|ГДЕ
	|	АналогиНормРасходов.Элемент = &Элемент
	|	И АналогиНормРасходовСрезПоследних.Норма > 0";
Запрос.УстановитьПараметр("НаДату", ТекущаяДата());
Запрос.УстановитьПараметр("Элемент", ВыбЭлемент);
Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ДобавитьПоПрименяемостиНаСервере(ВыборкаДетальныеЗаписи.ВладелецВладелец);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоПрименяемости(Команда)
ТаблицаНоменклатуры.Очистить();
ВыбМатериал = Неопределено;
	Если ВвестиЗначение(ВыбМатериал,"Выберите материал",Тип("СправочникСсылка.Материалы")) Тогда
	ДобавитьПоПрименяемостиНаСервере(ВыбМатериал);
	ДобавитьПоПрименяемостиАналогомНаСервере(ВыбМатериал);
	ТаблицаНоменклатуры.Сортировать("Товар,Номенклатура");
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьВСбыт(ЗапрещеноКОтгрузке)
БазаСбыта = ОбщийМодульСинхронизации.УстановитьCOMСоединение(Константы.БазаДанных1ССбыт.Получить());
	Если БазаСбыта = Неопределено Тогда
	Сообщить("Не открыто соединение с базой сбыта!");
	Возврат;
	КонецЕсли;
		Для каждого ТЧ Из ТаблицаНоменклатуры Цикл
			Если ТЧ.Пометка Тогда
				Если Не ТЧ.Товар.Пустая() Тогда
				Артикул = ОбщийМодульВызовСервера.ПолучитьАртикулПоКодуТовара(ТЧ.Товар.Код);
				бсНомен = БазаСбыта.Справочники.Номенклатура.НайтиПоРеквизиту("Артикул",Артикул);
					Если Не бсНомен.Пустая() Тогда
						Попытка
						бсНоменОбъект = бсНомен.ПолучитьОбъект(); 
						бсНоменОбъект.ОбменДанными.Загрузка = Истина;
						бсНоменОбъект.ОстановкаОтгрузки = ЗапрещеноКОтгрузке;
							Если ЗапрещеноКОтгрузке Тогда
							бсНоменОбъект.ДатаОстановкиОтгрузки = ТекущаяДата();
							Иначе	
							бсНоменОбъект.ДатаОстановкиОтгрузки = Дата(1,1,1);
							КонецЕсли;
						бсНоменОбъект.Записать();
						Исключение
						Сообщить(ОписаниеОшибки());
						КонецПопытки; 
					Иначе
					Сообщить("Товар с артикулом "+Артикул+" не найден в базе сбыта!");
					КонецЕсли;
				Иначе
				Сообщить(""+ТЧ.Номенклатура+" не привязана к товару!");
				КонецЕсли; 
			КонецЕсли;
		КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьЗапретКОтгрузкеНаСервере()
	Для каждого ТЧ Из ТаблицаНоменклатуры Цикл
		Если ТЧ.Пометка Тогда
		Номен = ТЧ.Номенклатура.ПолучитьОбъект();
		Номен.ЗапрещеноКОтгрузке = Истина;
		Номен.Записать();
		ТЧ.ЗапрещеноКОтгрузке = Истина;
		КонецЕсли;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗапретКОтгрузке(Команда)
УстановитьЗапретКОтгрузкеНаСервере();
ВыгрузитьВСбыт(Истина);
КонецПроцедуры

&НаСервере
Процедура СнятьЗапретКОтгрузкеНаСервере()
	Для каждого ТЧ Из ТаблицаНоменклатуры Цикл
		Если ТЧ.Пометка Тогда
		Номен = ТЧ.Номенклатура.ПолучитьОбъект();
		Номен.ЗапрещеноКОтгрузке = Ложь;
		Номен.Записать();
		ТЧ.ЗапрещеноКОтгрузке = Ложь;
		НаборЗаписей = РегистрыСведений.ДатыВыполненияПоПозициямЗНП.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.КодТовара.Установить(Номен.Товар.Код);
		НаборЗаписей.Прочитать();
			Для Каждого Запись Из НаборЗаписей Цикл 
			Запись.ЗапрещеноКОтгрузке = Ложь;
			КонецЦикла;
		НаборЗаписей.Записать(Истина);
		КонецЕсли;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура СнятьЗапретКОтгрузке(Команда)
СнятьЗапретКОтгрузкеНаСервере();
ВыгрузитьВСбыт(Ложь);
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗапрещенныеКОтгрузкеНаСервере()
ТаблицаНоменклатуры.Очистить();
Запрос = Новый Запрос;

Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ЗапрещеноКОтгрузке = ИСТИНА";
РезультатЗапроса = Запрос.Выполнить();
ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
	ТЧ = ТаблицаНоменклатуры.Добавить();
	ТЧ.Статус = ПолучитьСтатус(ВыборкаДетальныеЗаписи.Ссылка,ТекущаяДата());
	ТЧ.Номенклатура = ВыборкаДетальныеЗаписи.Ссылка;
	ТЧ.Товар = ВыборкаДетальныеЗаписи.Ссылка.Товар;
	ТЧ.ЗапрещеноКОтгрузке = ВыборкаДетальныеЗаписи.Ссылка.ЗапрещеноКОтгрузке;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЗапрещенныеКОтгрузке(Команда)
ПолучитьЗапрещенныеКОтгрузкеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВыбратьВсеНаСервере()
	Для каждого ТЧ Из ТаблицаНоменклатуры Цикл
	ТЧ.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
ВыбратьВсеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОтменитьВсеНаСервере()
	Для каждого ТЧ Из ТаблицаНоменклатуры Цикл
	ТЧ.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВсе(Команда)
ОтменитьВсеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТаблицаНоменклатурыНоменклатураПриИзмененииНаСервере(Стр)
ТЧ = ТаблицаНоменклатуры.НайтиПоИдентификатору(Стр);
ТЧ.Статус = ПолучитьСтатус(ТЧ.Номенклатура,ТекущаяДата());
ТЧ.Товар = ТЧ.Номенклатура.Товар;
ТЧ.ЗапрещеноКОтгрузке = ТЧ.Номенклатура.ЗапрещеноКОтгрузке; 
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНоменклатурыНоменклатураПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Элементы.ТаблицаНоменклатуры.ТекущиеДанные.Номенклатура) Тогда
	ТаблицаНоменклатурыНоменклатураПриИзмененииНаСервере(Элементы.ТаблицаНоменклатуры.ТекущаяСтрока);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ТаблицаНоменклатурыОбработкаВыбораНаСервере(ВыбранноеЗначение)
	Если ТаблицаНоменклатуры.НайтиСтроки(Новый Структура("Номенклатура", ВыбранноеЗначение)).Количество() = 0 Тогда
	ТЧ = ТаблицаНоменклатуры.Добавить();
	ТЧ.Номенклатура = ВыбранноеЗначение;
	ТЧ.Статус = ПолучитьСтатус(ТЧ.Номенклатура,ТекущаяДата());
	ТЧ.Товар = ТЧ.Номенклатура.Товар;
	ТЧ.ЗапрещеноКОтгрузке = ТЧ.Номенклатура.ЗапрещеноКОтгрузке; 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
ПараметрыПодбора = Новый Структура("ЗакрыватьПриВыборе, МножественныйВыбор", Ложь, Ложь);
ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыПодбора, Элементы.ТаблицаНоменклатуры);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНоменклатурыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
СтандартнаяОбработка = Ложь;
ТаблицаНоменклатурыОбработкаВыбораНаСервере(ВыбранноеЗначение);
КонецПроцедуры
